buildscript {
    repositories {
        mavenCentral()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.6'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard.flags'
        }
    }

    productFlavors {
        aosp {
            flavorDimensions 'default'
            applicationId 'com.aurora.launcher'
        }

        l3go {
            flavorDimensions 'default'
            applicationId 'com.aurora.launcher.go'
        }

        quickstep {
            flavorDimensions 'default'
            applicationId 'com.aurora.launcher.quickstep'
        }
    }

    sourceSets {
        main {
            res.srcDirs = ['res']
            java.srcDirs = ['src']
            manifest.srcFile 'AndroidManifest-common.xml'
            proto {
                srcDir 'protos/'
                srcDir 'proto_overrides/'
            }
        }

        aosp {
            java.srcDirs = ['src_flags', "src_ui_overrides"]
            manifest.srcFile "AndroidManifest.xml"
        }

        l3go {
            res.srcDirs = ['go/res']
            java.srcDirs = ['go/src_flags', "src_ui_overrides"]
            manifest.srcFile "go/AndroidManifest.xml"
        }

        quickstep {
            res.srcDirs = ['quickstep/res']
            java.srcDirs = ['src_flags', 'quickstep/src']
            manifest.srcFile "quickstep/AndroidManifest.xml"
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    google()
}

final String SUPPORT_LIBS_VERSION = '1.0.0'

dependencies {
    quickstepImplementation fileTree(dir: "quickstep/libs", include: 'sysui_shared.jar')
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.legacy:legacy-support-v4:${SUPPORT_LIBS_VERSION}"
    implementation "androidx.preference:preference:${SUPPORT_LIBS_VERSION}"
    implementation "androidx.dynamicanimation:dynamicanimation:${SUPPORT_LIBS_VERSION}"
    implementation "androidx.recyclerview:recyclerview:${SUPPORT_LIBS_VERSION}"
    implementation 'com.google.protobuf.nano:protobuf-javanano:3.2.0rc2'
}

protobuf {
    // Configure the protoc executable
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'
        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    remove java
                    javanano {
                        option "java_package=launcher_log_extension.proto|com.aurora.launcher.userevent.nano"
                        option "java_package=launcher_log.proto|com.aurora.launcher.userevent.nano"
                        option "java_package=launcher_dump.proto|com.aurora.launcher.model.nano"
                        option "enum_style=java"
                    }
                }
            }
        }
    }
}
